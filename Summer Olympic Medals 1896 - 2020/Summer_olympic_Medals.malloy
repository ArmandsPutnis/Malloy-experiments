source: raw_medals is duckdb.table('Summer_olympic_Medals.csv') extend {
  rename:
    host_country is Host_country
    host_city is Host_city
    country_name is Country_Name
    country_code is Country_Code
    gold is Gold
    silver is Silver
    bronze is Bronze
  dimension:
    is_host is pick 1 when country_name = host_country else 0
    # number = "####"
    olympic_year is `Year`
}

source: medals is raw_medals extend {
  measure:
    total_gold_medals is gold.sum()
    total_silver_medals is silver.sum()
    total_bronze_medals is bronze.sum()
    total_medals is gold.sum() + silver.sum() + bronze.sum()
    total_medal_points is gold.sum() * 3 + silver.sum() * 2 + bronze.sum()
    times_hosted is sum(is_host)
    # percent
    percentage_of_medals is total_medals / all(total_medals)

  view: year_country_top10_medals is {
    group_by:
      # number="####"
      `Olympic Year` is olympic_year
    aggregate: total_medals
      # list_detail 
      nest: Host is {
        group_by: host_country, host_city
      }
    nest: `Country Stats` is {
      top: 10
      group_by:
        country_name
        is_host is pick 'âœ“' when is_host = 1 else ''
      aggregate:
        percentage_of_medals
        total_medals
        Gold is total_gold_medals
        Silver is total_silver_medals
        Bronze is total_bronze_medals
    }
    order_by: `Olympic Year` desc
  }

  view: total_medals_year is {
    group_by: olympic_year
    aggregate:
      total_medals
    order_by: olympic_year
  }

  view: host is {
    group_by: `Year` is olympic_year
    where: is_host = 1
    order_by: `Year`
  }

  # bar_chart
    view: medals_type is {
    group_by: country_name
    # y
    aggregate:
      Gold is total_gold_medals
      Silver is total_silver_medals
      Bronze is total_bronze_medals
  }


-- best and worst year do not look Malloy-ish
  view: country_view is {
    group_by: country_name
    nest:host

    # detail
    nest: best_year is {
      group_by:  `Year` is olympic_year
      aggregate: 
        # percent
        `%` is percentage_of_medals
      order_by: `%` desc
      limit: 1
      }
      nest: worst_year is {
      group_by: 
        # number = "####"
        `Year`is olympic_year
      aggregate: 
        # percent
        `% ` is percentage_of_medals
      order_by: `% ` asc
      limit: 1
    }
    aggregate:
    total_medals
    nest:
      # line_chart
      total_medals_year
      medals_type
  }
}
